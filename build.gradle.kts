plugins {
    java
    `java-library`
    application
    id("com.github.johnrengelman.shadow") version("8.1.1")
}

group = "io.github.gaming32"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.ow2.asm:asm:9.6")

    compileOnlyApi("org.jetbrains:annotations:24.0.1")
}

//tasks.test {
//    useJUnitPlatform()
//}

application {
    mainClass.set("glang.launcher.ScriptLauncher")
}

tasks.shadowJar {
    manifest {
        attributes["Main-Class"] = application.mainClass.get()
    }
    for (pkg in listOf("org.objectweb.asm", "com.github.benmanes.caffeine")) {
        relocate(pkg, "glang.shaded.$pkg")
    }
}

val generateObjectInvokers by tasks.registering {
    val outputFile = file("src/main/java/glang/runtime/ObjectInvokers.java")
    val invokerCount = 17

    doLast {
        outputFile.printWriter().use { w ->
            w.println("package glang.runtime;")
            w.println()
            w.println("import glang.exception.UninvokableObjectException;")
            w.println("import glang.runtime.lookup.MethodLookup;")
            w.println()
            w.println("import java.util.List;")
            w.println()
            w.println("// WARNING: This class is generated by the generateObjectInvokers build task, DO NOT MODIFY.")
            w.println("// WARNING: If changes are needed, modify the task, not this file.")
            w.println("public final class ObjectInvokers {")
            w.println("    private ObjectInvokers() {")
            w.println("    }")

            // NOTE: This should be kept in sync, functionality-wise with GlangRuntime.invokeObject
            repeat(invokerCount) { argCount ->
                w.println()

                w.print("    public static Object invokeObject(Object target")
                repeat(argCount) { arg ->
                    w.print(", Object arg$arg")
                }
                w.println(") throws Throwable {")

                w.println("        if (target == null) {")
                w.println("            throw new NullPointerException(\"null is not invokable\");")
                w.println("        }")
                w.println("        if (target instanceof Class<?> clazz) {")
                w.println("            target = GlangRuntime.findConstructors(clazz);")
                w.println("        }")
                w.println("        if (target instanceof MethodLookup lookup) {")

                if (argCount > 0) {
                    w.print("            return lookup.getInvoker(List.of(")
                    repeat(argCount) { arg ->
                        if (arg > 0) {
                            w.print(", ")
                        }
                        w.print("\n                GlangRuntime.getClass(arg$arg)")
                    }
                    w.print("\n            )).invoke(")
                    repeat(argCount) { arg ->
                        if (arg > 0) {
                            w.print(", ")
                        }
                        w.print("arg$arg")
                    }
                    w.println(");")
                } else {
                    w.println("            return lookup.invoke();")
                }

                w.println("        }")
                w.println("        throw new UninvokableObjectException(\"Cannot invoke object of type \" + target.getClass().getName());")
                w.println("    }")
            }

            w.println("}")
        }
    }
}
